// Neighborhood-based collaborative filtering
1.0 : rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_items(I1,I2) >> rating(U,I2)^2
1.0 : rated(U,I1) & rated(U,I2) & rating(U,I2) & sim_items(I1,I2) >> rating(U,I1)^2

1.0 : rated(U1,I) & rated(U2,I) & rating(U1,I) & sim_users(U1,U2) >> rating(U2,I)^2
1.0 : rated(U1,I) & rated(U2,I) & rating(U2,I) & sim_users(U1,U2) >> rating(U1,I)^2

// Demographic and content based similarity
1.0 : rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_content_items(I1,I2) >> rating(U,I2)^2
1.0 : rated(U1,I) & rated(U2,I) & rating(U1,I) & sim_demo_users(U1,U2) >> rating(U2,I)^2

// Predictions by local non-negative matrix factorization and naive bayes predictor
1.0 : nmf_rating(U, I) & target(U, I) >> rating(U, I)^2
1.0 : rating(U, I) & target(U, I) >> nmf_rating(U, I)^2

1.0 : svd_rating(U, I) & target(U, I) >> rating(U, I)^2
1.0 : rating(U, I) & target(U, I) >> svd_rating(U, I)^2

1.0 : nb_rating(U, I) & target(U, I) >> rating(U, I)^2
1.0 : rating(U, I) & target(U, I) >> nb_rating(U, I)^2

// Mean-Centering
1.0 : avg_user_rating(U) & target(U,I) >> rating(U, I)^2
1.0 : rating(U, I) & target(U,I) >> avg_user_rating(U)^2

1.0 : avg_item_rating(I) & target(U,I) >> rating(U, I)^2
1.0 : rating(U, I) & target(U,I) >> avg_item_rating(I)^2

// Priors
1.0 : rating(U, I) = 1.0 ^2
1.0 : ~rating(U, I) ^2

// Value Unfairness
// Minimizes the inconsistency in signed estimation error across groups
// U_val = 1/n \sum_j |(average_predicted_rating_j_g1 - average_true_rating_j_g1 ) -
//                     (average_predicted_rating_j_g2 - average_true_rating_j_g2 )|

// Calculate average group item rating and preference
rating(+U, I) / @Max[1, |U|] = pred_group_average_item_rating(G, I) . {U: group_member(U, G) & target(U, I)}

// Value unfairness metric
1.0 : pred_group_average_item_rating(G1, I) - obs_group_average_item_rating(G1, I) = pred_group_average_item_rating(G2, I) - obs_group_average_item_rating(G2, I)
